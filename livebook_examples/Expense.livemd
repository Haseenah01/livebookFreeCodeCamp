# Expense

## Section

This creates a new date

```elixir
Date.new(2023, 04, 01)
```

Using pattern matching to make the right side = to the left side

```elixir
{:ok, date} = Date.new(2023, 04, 01)
```

This will return the value assigned to date variable

```elixir
date
```

```elixir
~D[2023-04-01]
```

`Enum.reduce` invokes the function for each element in the enumerable with the accumulator.

Capture operator `(&)` in Elixir is same as reference operator in other languages, with extended functionality.

`Enum.filter` filters the enumerable, i.e. returns only those elements for which function returns a truthy value (When an element has a truthy value (neither false nor nil) iteration stops immediately and true is returned).

`List.delete` deletes the given element from the list. Returns a new list without the element.

```elixir
defmodule Tutorials.Structs.Expense do
  alias Tutorials.Structs.Expense

  defstruct(
    title: "",
    date: nil,
    amount: 0,
    store: ""
  )

  @type t :: %Expense{
          title: String.t(),
          date: Date.t() | nil,
          amount: number(),
          store: String.t()
        }

  @spec sample :: [t()]
  def sample do
    [
      %Expense{title: "Grocery", date: ~D[2022-09-12], amount: 18.99, store: "Metro"},
      %Expense{title: "Mobile", date: ~D[2022-10-18], amount: 55.65, store: "Bell"},
      %Expense{title: "Jacket", date: ~D[2022-07-06], amount: 150.99, store: "Marks"},
      %Expense{title: "Air Jordans", date: ~D[2022-10-30], amount: 799.99, store: "Nike"}
    ]
  end

  @spec total([t]) :: number()
  def total(expenses) do
    expenses
    |> Enum.reduce(0, fn expense, acc -> expense.amount + acc end)
  end

  @spec sort_by_date([t()]) :: [t()]
  def sort_by_date(expenses) do
    expenses
    |> Enum.sort_by(& &1.date)
  end

  def add_expense(%Expense{} = expense) do
    [expense | sample()]
  end

  def update_amount(title, amount) do
    [item] = Enum.filter(sample(), fn %{title: expense_title} -> expense_title == title end)
    new_item = %{item | amount: amount}

    [new_item | sample() |> List.delete(item)]
  end

  # ------------------------With-------------------------------

  # login -> authenticate, verify_password
  @users ["Coco", "Cece", "Louis", "Chiko"]

  def authenticate(user) when user in @users, do: {:ok, "authorized"}
  def authenticate(_), do: {:error, "unauthorized"}

  def verify_password(user, _password) when user in @users, do: {:ok, "password verified"}
  def verify_password(_user, _password), do: {:error, "wrong password"}

  def login(user, password) do
    with {:ok, _auth_msg} <- authenticate(user),
         {:ok, _msg} <-
           verify_password(user, password) do
      {:ok, " #{user} logged in successfully"}
    else
      {:error, msg} -> {:error, msg}
      _ -> :unauthorized
    end
  end
end
```

```elixir
alias Tutorials.Structs.Expense
sample = Expense.sample()
```

```elixir
Expense.total(sample)
```

```elixir
Expense.sort_by_date(sample)
```

```elixir
expense = %Expense{title: "Coffee", date: ~D[2023-04-01], amount: 10, store: "Metro"}
```

```elixir
Expense.add_expense(expense)
```

```elixir
Expense.update_amount("Grocery", 30)
```

```elixir
Expense.login("Louis", 1234)
```

```elixir
Expense.login("Haseenah", 1234)
```
